\documentclass[fleqn,reqno]{amsart}
\usepackage{../lib/radoslav-macro}
\usepackage{../lib/radoslav-more}
\usepackage{times}
\usepackage{natbib}

\begin{document}
%% STRIP END



\begin{paragraf*}
This chapter is devoted to the computational aspects of our results.
Section~\ref{sec:algorithms} describes two algorithms for implicitization.
The first one is simple and robust, and is used for studying the matrices $N$
when Gr\"obner basis calculations can be carried out efficiently.
The second one is more involved and is used when direct computations are unfeasible.
In those cases, the second algorithm's lead is significant.
Section~\ref{sec:long-examples} provides details about the algorithms
and support to the latter claim in the form of a few worked examples.
\end{paragraf*}

\begin{paragraf*}
Both, as means to illustrate that our algorithms are effective, say,
in the sense of computational algebraic geometry,
and as a setup for the examples to follow,
in Section~\ref{sec:implementation} we implement those algorithms in
the Macaulay2 system.
\end{paragraf*}

\begin{paragraf*}
The code is available at
\begin{center}
	\url{http://www.math.cornell.edu/~rzlatev/phd-thesis}
\end{center}
\end{paragraf*}

\begin{paragraf*}
We continue to follow the notation of Chapter~\ref{ch:preliminaries}
and the setup of \eqref{par:setup}.
However, to avoid distraction,
we assume throughout the chapter that $\phi$ is generically 1-1.
\end{paragraf*}



\section{The Algorithms}
\label{sec:algorithms}

\begin{paragraf*}
At first glance, an algorithm for finding the implicit equation is contained in the proof
of the our main theorem, Theorem \ref{thm:gcd-minors}.
In its simplest form, it becomes
\end{paragraf*}

% \begin{absolutelynopagebreak}
\begin{algorithm} {\sc Naive Algorithm\bf.}
\label{algo:naive}
\begin{algorithmic}
  \State {\bf input:} $J$, $\d$
  \State {\bf output:} $N$, $P$
  \State Set $r=\dim_\CC(S_{\mathbf d})$
  \State Compute an $R$-generating set $\{h_\ell:\ell\}$ for the Rees ideal $I$
  \State Compute a $T$-generating set $\{g_k:k\}$ for $I_{\d,\bullet}$ from the $h_\ell$
  	using \eqref{lemma:push-gens}
  \State Set $N$ to be the coefficient matrix of the $g_k$ with respect to $\mt{basis}(S_\d)$
  \State Compute $P=\gcd(\mt{minors}(r,N))$
  \State Return $N$, $P$
\end{algorithmic}
\end{algorithm}
% \end{absolutelynopagebreak}

\begin{paragraf}
\label{par:flaws}
The conciseness and robustness of Algorithm \ref{algo:naive} made it our preferred tool
for testing the theory.
In fact, all calculations presented so far,
including all examples of Chapter \ref{ch:examples},
were carried out using this algorithm.

At the same time, its simplicity allows us to spot some of its drawbacks.
We distinguish four major ones.
\begin{enumerate}
\item
\label{itm:flaws:GB}
Computing an $R$-generating set for the Rees ideal is at least as hard as
computing the implicit equation itself --- we have $I_{0,\bullet}=P$.
This follows from Proposition~\ref{prop:deg-GB} and
shows up in Examples~\ref{ex202} and \ref{ex314}.

\item
\label{itm:flaws:many-minors}
While computing the $\gcd$ of two polynomials is fast,
computing all minors could be difficult since their number can be very large.
This happens even for reasonably small examples.
For instance, the smallest nonzero matrix $N$ for $\d=(2,2,1)$ in Example~\ref{ex603}
is of size $18\times50$.
The number of maximal minors is
\[
	\binom{50}{18}=18'053'528'883'775
\]
so even if it takes the unrealistic $0.001$ seconds to compute each minor,
a single machine would require 572 years to compute them all.

\item
\label{itm:flaws:large-det}
Continuing with Example~\ref{ex603},
we note that each maximal minor is a determinant of an $18\times18$-matrix
of quartic forms in 5 variables.
Computing large determinants symbolically is time-consuming.
We did not manage to compute any nonzero minor.

Example~\ref{ex602} involves a somewhat similar calculation ---
the determinant of a $12\times12$-matrix of quadratic forms in 5 variables
took about an hour to compute.
Extrapolating, we can speculate that our $18\times18$ determinant would take somewhere
in the order of
\[
	13\times14\times15\times16\times17\times18=13'366'080
\]
hours.
That is about $1525$ years.

\item
\label{itm:flaws:large-poly}
Finally, suppose we have found the polynomial in question --- by whatever means.
It is a form of degree $48$ in 5 variables, and very likely dense in the monomials of that degree.
This suggests that the polynomial will be represented by
\[
	\binom{53}{5}=2'869'685
\]
coefficients.
\end{enumerate}
\end{paragraf}

\begin{paragraf*}
Regrettably,
\itmref{itm:flaws:large-det}{par:flaws} would be an issue for any algorithm
relying on computing determinants of representation matrices,
while \itmref{itm:flaws:large-poly}{par:flaws}
would be an issue for any implicitization algorithm whatsoever.
Rather than seeing these as obstacles,
we point them out as an argument {\em for} the idea of using
representation matrices in place of the implicit equation altogether.
We explore this theme further in the examples of Section~\ref{sec:long-examples}.
\end{paragraf*}

\begin{paragraf}
\label{par:proposed}
Fix a degree $\d$ as before and recall that
\[
	N=(~N_1~|~\cdots~|~N_\delta~)
\]

Consider the following.
\begin{enumerate}
\item
\label{itm:N'}
Instead of computing the whole matrix $N$, one can compute the $N_i$'s separately,
keeping track of a partial representation matrix $N'$.
\item
\label{itm:gcd}
Instead of computing all the minors, one only needs to compute sufficiently many to determine
the $\gcd$ correctly.
\end{enumerate}
\end{paragraf}

\begin{paragraf*}
These two simple observations produce an immense speed up on average.
The advantage of \itmref{itm:N'}{par:proposed} over computing an $R$-generating set for the
Rees ideal is that it uses only linear algebraic routines.
The advantage of computing only sufficiently many, rather than all, of the minors is obvious.
\end{paragraf*}

\begin{paragraf}
Let
\[
	N'=N'_i=(~N_1~|~\cdots~|~N_i~)
\]
be the partial matrix of syzygies up to degree $i$.
Recall that $h_i$ denotes the number of columns in $N_i$
and set $p=\deg(P)$.
Consider the following condition
\begin{align}
	\label{C:matrix}
	\tag{C1}
	\begin{cases}
		\quad\sum h_ii=p &\text{if}~\sum h_i=r\\
		\quad\sum h_ii\geq p&\text{if}~\sum h_i>r
	\end{cases}
\end{align}
This is a necessary condition for $N'$ to be used instead of $N$,
since if $\deg(\det(M))<\deg(P)$, then $M$ must be singular.
It is almost certainly not a sufficient condition to conclude that $N'=N$,
but often this is the case.
\end{paragraf}

\begin{paragraf}
\label{par:intersect-general-line}
Let $M_1,M_2$ be nonsingular matrices of syzygies, as in \eqref{lemma:detM-P},
and let $Y_1,Y_2\se\PP^n$ be the hypersurfaces they define.
Let $L$ be a general line in $\PP^n$.
If $M_1,M_2$ satisfy the condition
\begin{align}
	\label{C:minors}
	\tag{C2}
	L\cap Y_1\cap Y_2\se L\cap Y
\end{align}
then $\gcd(\det(M_1),\det(M_2))=P$.

The condition can be used for testing \itmref{itm:gcd}{par:proposed}.
To prove the claim, note that we always have
\[
	Y_1\cap Y_2\supset V(\gcd(\det(M_1),\det(M_2)))\supset Y
\]
which together with \eqref{C:minors} gives $L\cap Y_1\cap Y_2=L\cap Y$,
which is true exactly when $Y_1\cap Y_2$ does not contain any other hypersurfaces besides $Y$.
Indeed, if the $\gcd$ is a proper multiple of $P$, then the intersection of
$Y_1$ and $Y_2$ contains another hypersurface, whose intersection with the general $L$
is not going to be on $Y$.

Furthermore, the condition itself can be tested by computing the multiplicity
of $Y_1\cap Y_2\cap L$ and comparing it to the multiplicity of $Y\cap L$.
If the two are equal, then the condition is satisfied.
\end{paragraf}

\begin{paragraf*}
Summarizing the discussion so far, we propose
\end{paragraf*}

\begin{algorithm}{\sc Proposed Algorithm}{\bf.}
\label{algo:proposed}
\begin{algorithmic}
\State{{\bf input:} $J$, $\d$, $p=\deg(P)$}
\State{{\bf output:} a list of matrices $M_k$ such that $\gcd(\{M_k:k\})=P$}
\State{Set $r=\dim_\CC(S_\d)$}
\State{Set $N'=r\times0$ matrix over $T$}
\State{}
\While{\eqref{C:matrix} is not satisfied for $N'$}
	\State{Given $N_1,\ldots,N_{i-1}$, use Algorithm \ref{algo:compute-Ni} to compute $N_i$}
	% \Comment{Call Recursion again}
	\State{Set $N'=N'~|~N_i$}
\EndWhile
\State{Set $M_1,M_2$ to be the zero $r\times r$ matrix}
\State{Let $\mathcal{C}$ be some stopping criterion}
\State{Let $i$ be the index of the last computed matrix and let $\ell=r-h_1-\ldots-h_{i-1}$}
\While{$\mathcal{C}$ is not satisfied and $M_1,M_2$ is do not satisfy \eqref{C:minors}}
	\State{Let $c_1,c_2$ be a random sets of $\ell$ columns of $N_i$}
	\State{Set $M_1=N_1|\cdots|N_{i-1}|(N_{i})_{c_1}$}
	\State{Set $M_2=N_1|\cdots|N_{i-1}|(N_{i})_{c_2}$}
\EndWhile
\State{Return $P=\gcd(\det(M_1),\det(M_2))$}
\end{algorithmic}
\end{algorithm}

\begin{algorithm}{\sc Compute Partial Syzygies}{\bf.}
\label{algo:compute-Ni}
\begin{algorithmic}
\State{{\bf input:} a list of the already computed $N_1,\ldots,N_{i-1}$}
\State{{\bf output:} $N_i$}
\For{$0<j<i$}
%	\State{Let $\mt{basis}(T)$ be the basis for $T_{i-j}$ as a row vector}
	\State{Set $N_{ji}=\mt{basis}(T_{i-j})\tensor N_{j}$}
	\State{Set $K_{ji}$ to be the linearization of $N_{ji}$}
\EndFor
\State{Set $K_i=\ker(\Phi^{(i)})$}
\State{Let $K_i'$ be such that $\Span(K_i)=\Span(K_i')\oplus(\sum_j\Span(K_{ji}))$}
\State{Let $N_i$ be such that $\mt{basis}(R_{\mathbf d,i})\cdot K_i'=\mt{basis}(S_{\mathbf d})\cdot N_i$}
\State{Return $N_i$}
\end{algorithmic}
\end{algorithm}



\section{Implementation in Macaulay2}
\label{sec:implementation}

\begin{paragraf}
\label{code:lemma:push-gens}
We start with a realization for \eqref{lemma:push-gens}.
\label{code:lemma:push-gens}
\begin{verbatim}
PushGens = (d,I) -> (
  r := toList(0..(#d-1));
  G := for g in I_* list (
    if all(d-((degree g)_r), Z->Z>=0)
    then basis((d-((degree g)_r))|{0},ring I)**g
    else continue);
  trim image fold(G,matrix {{0_(ring I)}},(a, b)->a|b)
  )
\end{verbatim}
\end{paragraf}

\begin{paragraf}
\label{code:algo:naive}
Using \eqref{code:lemma:push-gens},
Algorithm \ref{algo:naive} is straight-forward to implement.
We require~$R$ for encapsulation.
\begin{verbatim}
ComputeNRees = method ()
ComputeNRees (Ideal, List, Ring) := Matrix => (J, d, R) -> (
  x := symbol x;
  I := reesIdeal(J, Variable=>x);
  AI := ring I;
  zm := 0*d;
  g := map(R,AI,first entries super basis(zm|{1},R));
  I = g(I);
  V := PushGens (d,I);
  matrix entries ( (gens V) // basis(d|{0}, R) )
  )
\end{verbatim}
\end{paragraf}

\begin{paragraf}
\label{code:algo:compute-Ni}
Algorithm~\ref{algo:compute-Ni} is the one we make most use of in Section~\ref{sec:long-examples}.
Its fourth argument is the list of already computed matrices $N_1,\ldots,N_{i-1}$.
If this list's size is not $i-1$,
then we just compute all linearly independent syzygies of degree~$i$ --- $\basis(I_{\d,i})$.
The ideal $J$ is supplied in the form of the matrix
$F=\phi^{(1)}\tensor R$ (see \ref{par:strands}).
\begin{verbatim}
ComputeNi = method ()
ComputeNi (Matrix, ZZ, List, List) := Matrix => (F, i, d, lst) -> (
  R := ring F;
  m := #d;
  d0 := d|{0};
  di := d|{i};
  zm := 0*d;
  fj := flatten entries matrix F;
  xj := flatten entries super basis (zm|{1}, R);
  n := #fj;
  r := numcols super basis(d0, R);
  subs := apply(n,j->xj_j=>fj_j);
  e0 := (degree fj_0);
  G := sub(super basis(di, R), subs) // (super basis(i*e0+d0, R));
  K := matrix entries gens ker G;
  Nii := (super basis(di, R))*K // (super basis(d0, R));
  Nii = sub(matrix entries Nii,R);
  Nji := random(R^r,R^0);
  if #lst==i-1
  then Nji = fold (for j from 1 to #lst list
    (super basis(zm|{i-j}, R)**(lst_(j-1))), random(R^r,R^0), (m1,m2)->m1|m2);
  gens trim image (Nii%Nji)
  )
\end{verbatim}
\end{paragraf}

\begin{paragraf}
\label{code:algo:proposed}
We now implement the first part of the proposed Algorithm~\ref{algo:proposed}.
We omit the second part simply because of \itmref{itm:flaws:large-det}{par:flaws}.
Besides, after a general change of coordinates,
any two nonzero minors are likely to suffice and we can compute them manually when necessary.
See Examples~\ref{ex603} and \ref{ex604} for details.
\begin{verbatim}
ComputeNConj = method ()
ComputeNConj (Matrix, ZZ, List) := Matrix => (F, p, d) -> (
  R := ring F;
  q := 0;
  i := 1;
  lst := {};
  r := numcols super basis (d|{0},R);
  N := random(R^r,R^0);
  
  zm := 0*d;
  fj := flatten entries matrix F;
  xj := flatten entries super basis (zm|{1}, R);
  n := #fj;
  subs := apply(n,j->xj_j=>fj_j);
  
  while q<p or not procTestCondN(N, subs) do (
    Ni := ComputeNi(F,i,d,lst);
    q = q + (numcols Ni)*i;
    lst = append(lst, Ni);
    N = N|Ni;
    i = i+1; );
  N
  )
\end{verbatim}
\end{paragraf}



\section{Examples}
\label{sec:long-examples}

\begin{paragraf*}
We now field-test our algorithms and code on several examples of
somewhat higher computational complexity than those in Chapter~\ref{ch:examples}.

The running times can vary a lot from one machine to another,
so the numbers below should not be treated as benchmarks.
We include them only to provide a general idea how the different methods preform
relative to each other.

The machine that we used was a MacBook Pro laptop with
a 2.9 GHz Intel Core i7 processor and 8 GB 1600 MHz DDR3 memory,
running Macaulay2 version 1.8.
\end{paragraf*}

\begin{example}[$\mt{ex601}$]
\label{ex601}
Let $\phi:(\PP^1)^3\To\PP^4$ be given by 5 generic $(2,1,1)$-forms.
The base locus is empty,
so by \eqref{eq:formula-prod-P1} and \eqref{eq:degree-formula},
the degree of the image is $12$.

Consider $\d=(1,1,1)$.
Our method computes a candidate matrix $N'$ in a little more than 0.1s.
The matrix $N'$ is square and we have $\det(N')=P$.
Computing the determinant of the $8\times8$-matrix $N$ takes 3s.
The standard Gr\"obner basis computation takes 131s.

The details follow.
\begin{verbatim}
i1 : loadPackage "ImplicitizationAlgos";

i2 : KK=ZZ/32009;

i3 : S=KK[s_0,s_1,t_0,t_1,u_0,u_1,
          Degrees=>{2:{1,0,0},2:{0,1,0},2:{0,0,1}}];

i4 : T=KK[x_0..x_4];

i5 : B=super basis({2,1,1},S);

             1       12
o5 : Matrix S  <--- S

i6 : J=ideal(B*random(S^12,S^5));

o6 : Ideal of S

i7 : R=KK[s_0,s_1,t_0,t_1,u_0,u_1,x_0..x_4,
          Degrees=>{2:{1,0,0,0},2:{0,1,0,0},2:{0,0,1,0},5:{0,0,0,1}}];

i8 : F=sub(gens J,R);

             1       5
o8 : Matrix R  <--- R

i9 : d={1,1,1};

i10 : time N1=ComputeNi(F,1,d,{});
     -- used 0.0119639 seconds

              8       4
o10 : Matrix R  <--- R

i11 : time N2=ComputeNi(F,2,d,{N1});
     -- used 0.0920771 seconds

              8       4
o11 : Matrix R  <--- R
\end{verbatim}
The partial matrix $N'=N_1|N_2$ is square.
The degree of its determinant is $12=4\cdot1+4\cdot2$, so this is our candidate matrix.
We check at the end that $\det(N')=P(\mathbf x)$.
\begin{verbatim}
i12 : time ComputeNi(F,2,d,{});
     -- used 0.0867573 seconds

              8       24
o12 : Matrix R  <--- R

i13 : time N3=ComputeNi(F,3,d,{N1,N2});
     -- used 0.846328 seconds

              8
o13 : Matrix R  <--- 0

i14 : time ComputeNi(F,3,d,{});
     -- used 0.903612 seconds

              8       80
o14 : Matrix R  <--- R

i15 : time ComputeNi(F,4,d,{});
     -- used 6.38165 seconds

              8       200
o15 : Matrix R  <--- R
\end{verbatim}
Note that even though $N'$ is square and giving the implicit equation right off the bat,
we cannot be sure that $N=N_1|N_2$.
Lines 12--15 support this claim.
% First note that line 14 and 15 don't actually compute $N_i$ but rather $I_{\d,i}$.
For instance, because the columns of $N_1$ and $N_2$ form a nonsingular matrix,
the degree-$3$ syzygies they give rise to are going to be linearly independent.
There are exactly $80=4\cdot\binom{4+2}{2}+4\cdot\binom{4+1}{1}$ of them, so $N_3=0$.
The same argument shows that $N_4=0$.

\begin{verbatim}
i16 : N=sub(N1|N2,T);

              8       8
o16 : Matrix T  <--- T

i17 : time N'=ComputeNConj(F,12,d);
     -- used 0.138608 seconds

              8       8
o17 : Matrix R  <--- R

i18 : N'=sub(N',T);

              8       8
o18 : Matrix T  <--- T

i19 : image N==image N'

o19 = true

i20 : time P'=ideal det N;
     -- used 3.0447 seconds

o20 : Ideal of T
\end{verbatim}
Line 20 shows that $\det(N')=P(\mathbf x)$.
\begin{verbatim}
i21 : time P=ker map(S,T,J_*);
     -- used 131.05 seconds

o21 : Ideal of T

i22 : P==P'

o22 = true
\end{verbatim}
The standard method to compute the implicit equation takes more than 40 times longer.
\end{example}

\begin{example}[$\mt{ex602}$]
\label{ex602}
We compute the implicit equation of five general $(2,2,1)$-forms over $(\PP^1)^3$.
The base locus is empty, so the degree of the equation is $24$.
It takes our method less than a second to find it in the form of
a determinant of an $18\times18$-matrix of quadratic forms.
It take a little less than an hour to compute the actual equation.
The standard Gr\"obner basis calculation did not finish in 24 hours.
\begin{verbatim}
i1 : loadPackage "ImplicitizationAlgos";

i2 : KK=ZZ/32009;

i3 : S=KK[s_0,s_1,t_0,t_1,u_0,u_1,
          Degrees=>{2:{1,0,0},2:{0,1,0},2:{0,0,1}}];

i4 : T=KK[x_0..x_4];

i5 : B=super basis({2,2,1},S);

             1       18
o5 : Matrix S  <--- S

i6 : J=ideal(B*random(S^18,S^5));

o6 : Ideal of S

i7 : R=KK[s_0,s_1,t_0,t_1,u_0,u_1,x_0..x_4,
          Degrees=>{2:{1,0,0,0},2:{0,1,0,0},2:{0,0,1,0},5:{0,0,0,1}}];

i8 : F=sub(gens J,R);

             1       5
o8 : Matrix R  <--- R

i9 : d={2,1,1};

i10 : time N1=ComputeNi(F,1,d,{});
     -- used 0.017498 seconds

              12
o10 : Matrix R   <--- 0

i11 : time N2=ComputeNi(F,2,d,{});
     -- used 0.298143 seconds

              12       12
o11 : Matrix R   <--- R

i12 : time N3=ComputeNi(F,3,d,{N1,N2});
     -- used 4.01669 seconds

              12
o12 : Matrix R   <--- 0

i13 : time N'=ComputeNConj(F,24,d);
     -- used 0.762765 seconds

              12       12
o13 : Matrix R   <--- R

i14 : N'==N2

o14 = true

i15 : rank N2

o15 = 12
\end{verbatim}
Since we know that $N'=N_2$ is square,
its determinant is a form of degree $12\cdot2=24$,
so $\det(N')=P(\mathbf x)$.
Note that we do not have to compute the determinant to make sure that $N'$ is nonsingular.
We can compute the rank by substituting random numbers for the $x_j$
and computing over a finite field.
\begin{verbatim}
i17 : N=sub(N2,T);

              12       12
o17 : Matrix T   <--- T

i18 : time P'=det N;
     -- used 3310.29 seconds
\end{verbatim}
While we actually can compute the determinant, so also the implicit equation itself,
we could have answered questions like "Is this point on the image" using $N'$ only.
\end{example}

\begin{example}[$\mt{ex604}$]
\label{ex604}
Consider a map given by four general bi-quartics in the ideal $\la s^3,s^2t,t^2\ra$.
Then the base locus is the point $(0,0,1)$ of degree $5$ and multiplicity $6$.
The degree of the image is $26$.
Our method finds it in explicit form in about $6$ minutes,
while it takes us less than a minute to find two square matrices $M_1,M_2$
for which
\[
	\gcd(\det(M_1),\det(M_2))=P(\mathbf x)
\]

This highlights the interplay between the algebra and geometry~--- 
one of our initial goals.
The standard method takes more than 6 hours.
\begin{verbatim}
i1 : loadPackage "ImplicitizationAlgos";

i2 : KK=ZZ/32009;

i3 : S=KK[s,u,t,v,Degrees=>{2:{1,0},2:{0,1}}];

i4 : T=KK[x_0..x_3];

i5 : Z=ideal(s^3,s^2*t,t^2);

o5 : Ideal of S

i6 : B=super basis({4,4},Z);

             1       20
o6 : Matrix S  <--- S

i7 : J=ideal(B*random(S^20,S^4));

o7 : Ideal of S

i8 : decompose J

o8 = {ideal (s, t), ideal (s, u), ideal (v, t)}

o8 : List

i9 : multiplicity Z

o9 = 6
\end{verbatim}
The base locus is supported on a single point, so the Macaulay2 command
computes the correct quantity.
\begin{verbatim}
i10 : R=KK[s,u,t,v,x_0..x_3,
           Degrees=>{2:{1,0,0},2:{0,1,0},4:{0,0,1}}];

i11 : F=sub(gens J,R);

              1       4
o11 : Matrix R  <--- R

i12 : d={2,2};

i13 : time N1=ComputeNi(F,1,d,{});
     -- used 0.00821136 seconds

              9
o13 : Matrix R  <--- 0

i14 : time N2=ComputeNi(F,2,d,{N1});
     -- used 0.0901831 seconds

              9
o14 : Matrix R  <--- 0
\end{verbatim}
The degree $\d=(2,2)$ does not look promising.
Take $\d=(3,3)$ instead.
\begin{verbatim}
i15 : d={3,3};

i16 : time N1=ComputeNi(F,1,d,{});
     -- used 0.0208163 seconds

              16       5
o16 : Matrix R   <--- R

i17 : time N2=ComputeNi(F,2,d,{N1});
     -- used 0.221774 seconds

              16       12
o17 : Matrix R   <--- R

i18 : N=N1|N2;

              16       17
o18 : Matrix R   <--- R

i19 : time rank N
     -- used 0.009011 seconds

o19 = 16
\end{verbatim}
This is our candidate matrix $N'$ --- it has more columns than rows and any
nonzero minor would have degree $27$ or $28$.
Having $N'$ be of full rank is not enough by itself.
\begin{verbatim}
i20 : c1=sort join({0,1,2,3,4}, RandPerm(5,16,11));

i21 : time rank N_c1
     -- used 0.00241363 seconds

o21 = 16

i22 : c2=sort join({0,1,2,3,4}, RandPerm(5,16,11));

i23 : c1==c2

o23 = false

i24 : time rank N_c2
     -- used 0.00881294 seconds

o24 = 16
\end{verbatim}
At this point we found two distinct length-$12$ sets of columns, $c_1,c_2$,
such that the square matrices $M_1,M_2$ they give rise to are nonsingular.
We can show that these satisfy the equality described above.
However, we can actually compute the implicit equation in this case.
\begin{verbatim}
i25 : time D1=det N_c1;
     -- used 164.479 seconds

i26 : time D2=det N_c2;
     -- used 162.898 seconds

i27 : time P=gcd(D1,D2);
     -- used 0.0646495 seconds

i28 : deg P

o28 : 26
\end{verbatim}
\end{example}

\begin{example}[$\mt{ex603}$]
\label{ex603}
We enhance Example~\ref{ex602} and compute the implicit equation of five general
$(2,2,2)$-forms on $(\PP^1)^3$.
The base locus is again empty, so the degree of the image is $48$.

In this setup there is no hope of computing any determinant explicitly,
let alone computing $P(\mathbf x)$ using Gr\"obner bases directly.
We can, however, repeat what we did in Example~\ref{ex604}.
We work over $\d=(2,2,1)$ and find two square matrices $M_1,M_2$ of order $18$
such that
\[
	\gcd(\det(M_1),\det(M_2))=P(\mathbf x)
\]
While testing if the matrices are nonsingular is easy,
checking whether their determinants do not have any other prime common factor
is much harder.
We follow the discussion in~\eqref{par:intersect-general-line} and find
that the common intersection locus of the two hypersurfaces with a general line
has multiplicity $48$.
\begin{verbatim}
i1 : loadPackage "ImplicitizationAlgos";

i2 : KK=ZZ/32009;

i3 : S=KK[s_0,s_1,t_0,t_1,u_0,u_1,Degrees=>{2:{1,0,0},2:{0,1,0},2:{0,0,1}}];

i4 : T=KK[x_0..x_4];

i5 : B=super basis({2,2,2},S);

             1       27
o5 : Matrix S  <--- S

i6 : J=ideal(B*random(S^27,S^5));

o6 : Ideal of S

i7 : R=KK[s_0,s_1,t_0,t_1,u_0,u_1,x_0..x_4,
         Degrees=>{2:{1,0,0,0},2:{0,1,0,0},2:{0,0,1,0},5:{0,0,0,1}}];

i8 : F=sub(gens J,R);

             1       5
o8 : Matrix R  <--- R

i9 : d={1,1,1};

i10 : N1=ComputeNi(F,1,d,{});

              8
o10 : Matrix R  <--- 0

i11 : N2=ComputeNi(F,2,d,{});

              8
o11 : Matrix R  <--- 0

i12 : N3=ComputeNi(F,2,d,{});

              8
o12 : Matrix R  <--- 0
\end{verbatim}
We sneak in a remark here.
One might expect to find that $N_6$ is square, so that $\deg(\det(N_6))=48$.
In fact $N_i=0$ for up to at least $i=8$.
In general, smaller $\d$ tend to produce the few matrices $N_i$ zero.
This follows from the sort of Koszul-ness of small syzygies.
Take $\d=(2,2,1)$ instead.
\begin{verbatim}
i13 : d = {2,2,1};

i14 : time N4=ComputeNi(F,4,d,{});
     -- used 151.652 seconds

              18       50
o14 : Matrix R   <--- R

i15 : time rank N4
     -- used 2.2373 seconds

o15 = 18

i16 : c1=RandPerm(0,49,18);

i17 : M1=N4_c1;

              18       18
o17 : Matrix R   <--- R

i18 : rank M1

o18 = 18

i19 : c2=RandPerm(0,49,18);

i20 : M2=N4_c2;

              18       18
o20 : Matrix R   <--- R

i21 : rank M2

o21 = 18

i22 : sort c1==sort c2

o22 = false
\end{verbatim}
The two matrices are nonsingular.
\begin{verbatim}
i23 : M1=sub(M1,T);

              18       18
o23 : Matrix T   <--- T

i24 : M2=sub(M2,T);

              18       18
o24 : Matrix T   <--- T

i25 : L=ideal random(T^{1,1,1},T^1);

o25 : Ideal of T
\end{verbatim}
Here $L$ or rather $V(L)$ is the generic line.
\begin{verbatim}
i26 : TL=T/L;

i27 : super basis(1,TL)

o27 = | x_3 x_4 |

               1        2
o27 : Matrix TL  <--- TL

i28 : U=KK[y_0,y_1];

i29 : g=map(U,TL,{0,0,0,y_0,y_1});

o29 : RingMap U <--- TL

i30 : M1'=g(sub(M1,TL));

              18       18
o30 : Matrix U   <--- U

i31 : M2'=g(sub(M2,TL));

              18       18
o31 : Matrix U   <--- U
\end{verbatim}
This last bit is for computational purposes only.
Since we are on a $\PP^1$, we should make use of this and work over 2 variables only.
\begin{verbatim}
i32 : time D1=det(M1');
     -- used 0.317001 seconds

i33 : time D2=det(M2');
     -- used 0.334676 seconds

i34 : degree gcd(D1,D2)

o34 = {48}

o34 : List
\end{verbatim}
The multiplicity of the common intersection is $48$! We are done.
\end{example}



%% STRIP BEGIN
%% BIBLIOGRAPHY
\bibliographystyle{unsrtnat}
\bibliography{../lib/refs}

\end{document}